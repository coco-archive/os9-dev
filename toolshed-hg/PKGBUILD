# Maintainer: Paul Hentschel <aur at hpminc dot com>

pkgname=toolshed-hg
pkgver=r674.cc0c69db5793
pkgrel=1
pkgdesc='Utilities for Tandy Color Computer and Dragon microcomputers cross-development.'
arch=('x86_64')
url='http://toolshed.sourceforge.net'
license=('PerlArtistic')
groups=('os9-dev')
depends=('fuse2')
makedepends=('mercurial' 'discount')
conflicts=("${pkgname%-hg}")
provides=("${pkgname%-hg}")
source=("${pkgname%-hg}::hg+http://hg.code.sf.net/p/toolshed/code")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname%-hg}/build/unix"
  sed -i '/(CC)/ s/$/ $(LDFLAGS)/' ar2/Makefile
  sed -i '/(CC)/ s/$/ $(LDFLAGS)/' makewav/Makefile
}

pkgver() {
  cd "${pkgname%-hg}"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)" | sed 's/+//g'
}

build() {
  cd "${pkgname%-hg}"
  make -C build/unix 
}

check() {
  cd "${pkgname%-hg}"
  tests/hybrid-dsk.sh
  tests/multihdb-dsk.sh
}

package() {
  cd "${pkgname%-hg}"
  make DESTDIR="$pkgdir/" -C build/unix install

  # Install license files
  sed -n '/Copyright/,/PARTICULAR PURPOSE./p' casm/src/util.h > LICENSE
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  
  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir/usr/share/doc/${pkgname%-hg}/"
}

# vim:set ts=2 sw=2 et:
