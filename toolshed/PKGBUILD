# Maintainer: Paul Hentschel <youremail@domain.com>

pkgname=toolshed
pkgver=2.2
pkgrel=1
pkgdesc="Utilities for Tandy Color Computer and Dragon microcomputers cross-development."
arch=('x86_64')
url="http://toolshed.sourceforge.net"
license=('custom')
groups=('os9-dev')
makedepends=('discount')
source=("https://launchpad.net/~tormodvolden/+archive/ubuntu/m6809/+files/${pkgname}_${pkgver}.orig.tar.gz"
        "copyright")
sha256sums=('ab7201970e9c13e1cbdb9c8398bd888f870e75e33055b7a308bb039f67614c69'
            'd8c35bfb5ed2c18e21303c263786788ff8b4b25a7ce940cf3d108b4e7e33d645')

build() {
  cd "$pkgname-$pkgver"
  make -C build/unix 
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build/unix install

  # Install license file
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/copyright" 
  
  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
