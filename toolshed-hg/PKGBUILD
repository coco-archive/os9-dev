# Maintainer: Paul Hentschel <paul at hpminc dot com>

pkgname=toolshed-hg
pkgver=r673.6906ea14f8f5
pkgrel=1
pkgdesc='Utilities for Tandy Color Computer and Dragon microcomputers cross-development.'
arch=('x86_64')
url='http://toolshed.sourceforge.net'
license=('PerlArtistic')
groups=('os9-dev')
depends=('glibc')
makedepends=('mercurial' 'discount')
conflicts=("${pkgname%-hg}")
provides=("${pkgname%-hg}")
source=("${pkgname%-hg}::hg+http://hg.code.sf.net/p/toolshed/code")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-hg}"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

build() {
  cd "$srcdir/${pkgname%-hg}"
  make -C build/unix 
}

check() {
  cd "$srcdir/${pkgname%-hg}"
  tests/hybrid-dsk.sh
  tests/multihdb-dsk.sh
}

package() {
  cd "$srcdir/${pkgname%-hg}"
  make DESTDIR="$pkgdir/" -C build/unix install

  # Install license files
  sed -n '/Copyright/,/PARTICULAR PURPOSE./p' casm/src/util.h > LICENSE
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  
  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir/usr/share/doc/${pkgname%-hg}/"
}

# vim:set ts=2 sw=2 et:
