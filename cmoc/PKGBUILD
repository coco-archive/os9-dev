# Maintainer: Paul Hentschel <aur at hpminc dot com>

pkgname=cmoc
pkgver=0.1.57
pkgrel=1
pkgdesc="6809-generating cross-compiler for a subset of the C language."
arch=('x86_64')
url="http://perso.b2b2c.ca/~sarrazip/dev/cmoc.html"
license=('GPL3')
groups=('os9-dev')
depends=('perl' 'lwtools')
makedepends=('discount')
source=("http://perso.b2b2c.ca/~sarrazip/dev/$pkgname-$pkgver.tar.gz"
        "doc_fix.patch")
sha256sums=('011a45b916d00530cd03605a65f78de9270f2fda808c5dec8c3c7fdd604762c9'
            '17a68d9aea892b5563b0df9d5138a5d5e7ed02fc46d0b89b702c7679a0a4f21d')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/doc_fix.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make && make html
}

check() {
  cd "$pkgname-$pkgver"
  make  -k check -j1
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install html documentation
  install -m644 doc/cmoc-{implementation,manual,vectrex}.html "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
