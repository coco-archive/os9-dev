# Maintainer: Paul Hentschel <paul at hpminc dot com>

pkgname=gcc6809
pkgver=4.6.4
pkgrel=1
pkgdesc="The GNU C compiler for the M6809 architecture."
arch=('x86_64')
url="http://ppa.launchpad.net/tormodvolden/m6809/ubuntu/pool/main/g/$pkgname"
license=('GPL3')
groups=('os9-dev')
depends=('libmpc')
makedepends=('lwtools')
source=("$url/${pkgname}_$pkgver.orig.tar.gz"
        "gcc6809lw-4.6.4-9.patch"
        "z1_backported_cprop_ff5a75fc.patch"
        "z99_bump-version.patch"
        "gcc6809-texi.patch")
sha256sums=('8b6e03ba5ead825b98cff228465f20e8ccd1bbd3f04e3d85cf71c9c13e089289'
            '33d7f6e60ff5a528271b72bce26e5bb5157a88cedd5a904a9984b9d02205eeef'
            '54ef4662457fa5604cad0afee60840c7badcf92ceaca11fca9fe5e21df1fa7c6'
            'c5319f8ea8900b66c4bd4b0decd3a4cbc591d18578ca6546a7a1818795f77251'
            '95698b2f78fdf8a2b5220acae70e9dde253fbec6441fc9d919f85ea91be06646')

prepare() {
  if [[ -d "$pkgname-$pkgver" ]]; then
    rm -rf "$pkgname-$pkgver"
  fi
  mv gcc/ "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/gcc6809lw-4.6.4-9.patch
  patch -p1 -i "$srcdir"/z1_backported_cprop_ff5a75fc.patch
  patch -p1 -i "$srcdir"/z99_bump-version.patch
  patch -p1 -i "$srcdir"/gcc6809-texi.patch
}

build() {
  cd "$pkgname-$pkgver"
  if [[ -d 'build-gcc' ]]; then
    rm -rf build-gcc
  fi
  mkdir -v build-gcc
  cd build-gcc
  CPPFLAGS=""
  CXXFLAGS="-O2 -Wno-error=format-security"
  CFLAGS="-O2 -Wno-error=format-security"
  ../configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-languages=c \
    --srcdir=.. \
    --target=m6809-unknown \
    --disable-libada \
    --program-prefix=m6809-unknown- \
    --enable-obsolete \
    --disable-threads \
    --disable-nsl \
    --disable-libssp \
    --with-as=/usr/bin/m6809-unknown-as \
    --with-ld=/usr/bin/m6809-unknown-ld \
    --with-ar=/usr/bin/m6809-unknown-ar

  make all-gcc all-target-libgcc
}

#check() {
#  cd "$pkgname-$pkgver/build-gcc"
#  make -k check-gcc
#}

package() {
  CPPFLAGS=""
  CFLAGS="-O2"
  cd "$pkgname-$pkgver/build-gcc"
  make DESTDIR="$pkgdir/" install-gcc install-target-libgcc

  # Add prefix to info pages to avoid conflict
  mv "$pkgdir/usr/share/info/cpp.info" \
     "$pkgdir/usr/share/info/m6809-unknown-cpp.info"
  mv "$pkgdir/usr/share/info/cppinternals.info" \
     "$pkgdir/usr/share/info/m6809-unknown-cppinternals.info"
  mv "$pkgdir/usr/share/info/gcc.info" \
     "$pkgdir/usr/share/info/m6809-unknown-gcc.info"
  mv "$pkgdir/usr/share/info/gccinstall.info" \
     "$pkgdir/usr/share/info/m6809-unknown-gccinstall.info"
  mv "$pkgdir/usr/share/info/gccint.info" \
     "$pkgdir/usr/share/info/m6809-unknown-gccint.info"

  # use our own man page section to avoid conflicts
  mv "$pkgdir/usr/share/man/man7/gpl.7" \
     "$pkgdir/usr/share/man/man7/gpl.7gcc6809"
  mv "$pkgdir/usr/share/man/man7/gfdl.7" \
     "$pkgdir/usr/share/man/man7/gfdl.7gcc6809"
  mv "$pkgdir/usr/share/man/man7/fsf-funding.7" \
     "$pkgdir/usr/share/man/man7/fsf-funding.7gcc6809"

  # empty folders
  cd "$pkgdir/usr"
  rmdir -p m6809-unknown/lib
  rmdir include
}

# vim:set ts=2 sw=2 et:
